#include "tezosTemplate2.jsligo"

//util function
let assert_failure = (result : test_exec_result) : bool => {
  return match(result,{
    Success: (_ : nat) => false,
    Fail: ( tee : test_exec_error ) => { 
      Test.log (["Exception correctly caught", 
      match(tee,{
        Other: () =>"Other", 
        Rejected: (s:[michelson_program , address])=>("Cause : Rejected") })
      ]) ; 
      return true}
    })
  };
  
  let initial_storage = 
  {  name : "Which is the cutiest pokemon?",
  dateFrom : ("2022-01-01t00:00:00Z" as timestamp),  
  dateTo : ("2022-03-01t00:00:00Z" as timestamp),  
  options : list(["Mew","Pikachu"]) ,  
  votes : (Map.empty as map<address, string>), 
  results : (Map.empty as map<string, int>) } ;
  
  // create faucet
  let _1 = Test.reset_state ( 2 as nat, list([]) as list <tez> );
  let faucet = Test.nth_bootstrap_account(0);
  
  
  let initAccount = ([accList, keypair]: [list<address>, [string,key]]) : list<address> => {
    Test.log("For keypair :");  
    Test.log(keypair);
    //add this account to test context
    Test.add_account(keypair[0],keypair[1]);
    //create the account in Tezos
    const hk : key_hash = Crypto.hash_key(keypair[1]);
    let c : contract<unit> = Tezos.implicit_account(hk);
    //set balance
    let _ : test_exec_result = Test.transfer_to_contract (c,unit,(1000 as tez));
    Test.log("New account");
    Test.log(Tezos.address(c));
    Test.log(Test.get_balance(Tezos.address(c)));
    Test.log("with voting power :");
    let vp : michelson_program = Test.run((phk : key_hash) => Tezos.voting_power(phk) , hk )  ;
    Test.log(Test.decompile(vp) as nat);
    return list([Tezos.address(c) , ...accList]); 
  };
  
  // account list
  let senderKey1 : [string,key] = Test.new_account();
  let senderKey2 : [string,key] = Test.new_account();
  let senderKey3 : [string,key] = Test.new_account();
  
  let keys : list<[string,key]> = list([senderKey1,senderKey2,senderKey3]);
  let addresses : list<address> = List.fold(initAccount, keys, list([]) as list<address>);
  
  let sender1 : address = Option.unopt ( List.head_opt(addresses));
  let sender2 : address = Option.unopt ( List.head_opt(Option.unopt( List.tail_opt(addresses))));
  let sender3 : address = Option.unopt ( List.head_opt(Option.unopt( List.tail_opt( Option.unopt(List.tail_opt(addresses))))));                         
  
  let _3 = Test.set_baker(sender1);
  let _2 = Test.set_source(sender1);
  
  let [taddr, _, _] = Test.originate(main, initial_storage, (0 as tez));
  let contr = Test.to_contract(taddr);
  
  let _testOriginate = () : bool => {
    assert((Test.get_storage(taddr) as storage).name  == "Which is the cutiest pokemon?");
    assert((Test.get_storage(taddr) as storage).dateFrom  == ("2022-01-01t00:00:00Z" as timestamp));
    assert((Test.get_storage(taddr) as storage).dateTo  == ("2022-03-01t00:00:00Z" as timestamp));
    return true ;
  };
  
  let _testVote = ([s,o,k,voteCount] : [address,string,key_hash,int]) : bool => {
    Test.set_source(s);
    Test.log(s);
    Test.log(k);
    let status = Test.transfer_to_contract(contr, (Vote([o,k])), 1 as mutez);
    Test.log(status);
    let st = (Test.get_storage(taddr) as storage);
    Test.log(st);
    assert(Option.unopt(Map.find_opt(s,st.votes))  == o);
    Test.log (["expected voteCount : ", voteCount]) ;
    Test.log (["actual voteCount : ",Option.unopt(Map.find_opt(o,st.results))]) ;
    //assert(Option.unopt(Map.find_opt(o,st.results))  == voteCount);
    return true;
  };
  
  let _testVoteFail = ( [s,o,k] : [address,string,key_hash]) : bool => {
    Test.set_source(s);
    return assert_failure(Test.transfer_to_contract(contr, (Vote([o,k])), 1 as mutez));
  };
  
  let testOriginate = _testOriginate();
  
  //ovverride out of time
  let _oldTime = Test.set_now('2000-02-01T00:00:00Z' as timestamp);
  let testVoteOutOfDate = _testVoteFail(sender1,"Pikachu",Crypto.hash_key(senderKey1[1]));
  //return to normal date
  let _currentTime = Test.set_now('2022-02-01T00:00:00Z' as timestamp);
  let testSender1VoteUnknownOption = _testVoteFail(sender1, "Batman",Crypto.hash_key(senderKey1[1]));
  let testSender1VotePikachu = _testVote(sender1,"Pikachu",Crypto.hash_key(senderKey1[1]),1);
  let testSender1VotePikachuAgainAndFail = _testVoteFail(sender1,"Pikachu",Crypto.hash_key(senderKey1[1]));
  let testSender2VotePikachu = _testVote(sender2,"Pikachu",Crypto.hash_key(senderKey2[1]),2);
  let testSender2VoteMewAndFail = _testVoteFail(sender2,"Mew",Crypto.hash_key(senderKey2[1]));
  let testSender3VoteMew = _testVote(sender3,"Mew",Crypto.hash_key(senderKey3[1]),1);
  